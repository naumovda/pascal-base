# Стандарт стилевого оформления исходного кода лабораторных работ

## Введение
 
Этот стандарт документирует стилевое оформление для форматирования исходного кода лабораторных работ на языке Pascal (Free Pascal). Оригинал статьи создан Чарльзом Калвертом и расположен на "Borland Community site". В стандарте использованы материалы команды разработчиков Delphi, сообщества разработчиков библиотеки JEDI. Стандарт содержит правила, актуальные для студентов, изучающих курс "Программирование и алгоритмические языки".

Процесс перехода с собственного стиля оформления на предлагаемый может оказаться непростым, но человеческий мозг довольно легко адаптируется к стандартам и находит пути для быстрого запоминания предлагаемых правил. В дальнейшем, следование стандарту не вызывает затруднений.

## Файлы исходного кода 

Исходный код подразделяется на модули и файлы проекта, которые подчиняются одинаковым соглашениям. Файл проекта имеет расширение LPR (при работе в среде Lazarus). Этот файл является главным исходным файлом для всего проекта. Любые модули, используемые в проекте, будут иметь расширение PAS.

## Именование исходных файлов

Язык Pascal поддерживает длинные имена файлов. Если при создании имени Вы используете несколько слов, то необходимо использовать заглавную букву для каждого слова в имени: `MyLongName.pas`. Этот стиль оформления известен как InfixCaps или CamelCaps. Расширения файлов должны быть в нижнем регистре. 

## Организация исходных файлов	

Все программы Pascal могут содержать следующие элементы в определенном порядке:
- Информация о правах (Copyright/ID);
- Имя программы (Program Name);
- Объявление используемых модулей (Uses clause);
- Раздел определения констант/типов данных/процедур и функций/переменных;
- Раздел операторов (begin - end.).

Для визуального разделения элементов между ними должна быть хотя бы одна пустая строка.

Пример заголовка для модуля: 
```pascal
{************************************************************}
{                                                            }
{                 Модуль ХХХ                                 }
{       Copyright (c) 2001  ООО ХХХХ                         }
{               отдел/сектор                                 }
{                                                            }
{  Разработчик: ХХ ХХ                                        }
{  Модифицирован: 25 июня 2001                               }
{                                                            }
{************************************************************}
```

Следует обратить внимание на элементы заголовка:
- Назначение модуля;
- Копирайт;
- Разработчик;
- Дата последней модификации для исполняемой версии.

## Объявление программы

Каждый исходный файл должен содержать объявление программы. Слово `program` является ключевым, поэтому оно должно быть написано в нижнем регистре. Имя программы может содержать символы как в верхнем, так и в нижнем регистре и должно быть таким же, как и имя используемое для этого файла операционной системой. Например:
`program IterationCycles;`

Этот файл будет назван IterationCycles.lpr, когда он будет сохранен операционной системой.

## Соглашение об именованиях

Исключая зарезервированные слова и директивы, которые всегда пишутся в нижнем регистре, все идентификаторы  Pascal должны использовать InfixCaps:
`MyIdentifier MyFTPClass;`

Для разделения слов в идентификаторе нельзя использовать символ подчеркивания. Имя константы, переменной и имя типа должны быть именами существительными или фразой с именем существительным.

Правильно:
```
AddressForm 
ArrayIndexOutOfBoundsException 
```

Неправильно:
```
ManageLayout (глагол) 
delphi_is_new_to_me (подчеркивание) 
```

Исключение для Венгерской нотации делается в случае объявления перечислимого типа:
```
TBitBtnKind = (bkCustom, bkOK, bkCancel, bkHelp, 
    bkYes, bkNo, bkClose, bkAbort, bkRetry,
    bkIgnore, bkAll);
```    
`bk` обозначает `ButtonKind`.

Следует избегать однобуквенных имен, кроме как для временных переменных и переменных цикла.
Переменные цикла именуются `i` и `j`. Другие случаи использования однобуквенных переменных это `s` (строка) и `r` (результат). Не рекомендуется использовать переменную `l` (эль), потому что она похожа на `1` (единица).

Зарезервированные слова и директивы должны быть все в нижнем регистре. Стандартные типы должны начинаться с большой буквы (Integer), однако `string` - это зарезервированное слово и оно должно быть в нижнем регистре.

Все объявления типов данных должны начинаться с префикса `Т` и должны придерживаться правил, приведенных при описании оформления модуля.

## Использование пробелов и пустых строк

Пустые строки могут повысить читабельность путем группирования секций кода, которые логически связаны между собой. Пустые строки должны использоваться в следующих местах:
- После блока копирайта.
- После каждого раздела описаний.

Пробелы, запрещенные к использованию
- До или после оператора .(точка);
- Между именем метода и открывающей скобкой;
- Между унарным оператором и его операндом;
- После открывающей скобки или перед закрывающей;
- После открывающей квадратной скобки [ или перед закрывающей ];
- Перед точкой с запятой.

Примеры правильного использования:
```
function TMyClass.MyFunc(var Value: Integer);
MyPointer := @MyRecord;
MyClass := TMyClass(MyPointer);
MyInteger := MyIntegerArray[5];
```

Примеры неправильного использования:
```
function TMyClass.MyFunc( var Value: Integer ) ;
MyPointer := @ MyRecord;
MyClass := TMyClass ( MyPointer ) ;
MyInteger := MyIntegerArray [ 5 ] ;
```

## Использование отступов

Всегда необходимо использовать два пробела для всех уровней отступа. Другими словами, первый уровень отступает на два пробела, второй на четыре и так далее. Никогда не используйте символы табуляции.

Существует несколько исключений из этого правила. Зарезервированные слова `unit`, `uses`, `type`, `interface`, `implementation`, `initialization` и `finalization` всегда должны примыкать к левой границе. 

В файле проекта выравнивание по левой границе применяется к словам program, главным `begin` и `end`. Код внутри блока `begin..end` должен иметь отступ два символа.

## Перенос строк

Все строки должны быть ограничены 80 столбцами. Строки, длиннее чем 80 столбцов должны быть разделены и перенесены. Все перенесенные строки должны быть выровнены по первой строке и иметь отступ в два символа. Выражение begin всегда должно находиться на своей отдельной строке.

Нельзя переносить строки в тех местах, где не допускаются пробелы, например между именем метода и открывающей скобкой или между именем массива и открывающей квадратной скобкой. Никогда нельзя помещать выражение `begin` на строку, содержащую другой код.

Неправильно
```
while (LongExpression1 or LongExpression2) do begin
  // DoSomething
  // DoSomethingElse;
end;
```

Правильно
```
while (LongExpression1 or LongExpression2) do 
begin
  // DoSomething
  // DoSomethingElse;
end;
```
В случае с логическими операторами предпочтительнее будет следующий вариант:
```
if (LongExpression1) 
  or (LongExpression2) 
  or (LongExpression3) then 
```  

## Комментарии 

Язык Pascal поддерживает два типа комментариев: блочные и однострочные. Общие соображение по использованию комментариев могут быть следующими:
- Помещайте комментарий недалеко от начала модуля для пояснения его назначения;
- Помещайте комментарий перед объявлением процедур и функций;
- Избегайте очевидных комментариев: (i := i + 1 // добавить к i единицу);
- Помните, что вводящий в заблуждение комментарий хуже чем его отсутствие;
- Избегайте помещать в комментарий информацию, которая со временем может быть не верна.

## Операторы

Операторы - это одна или более строк кода, разделенных точкой с запятой. Простые операторы имеют одну точку с запятой, а составные могут иметь более чем одну точку с запятой и, таким образом, состоят из множества простых операторов.

Это простой оператор:
`A := B;`

Это составной или структурированный оператор:
```
begin
  B := C;
  A := B;
end;
```

Простые операторы содержат одну точку с запятой. Если Вам необходимо разделить операторы, то перенесите продолжение оператора на следующую строку с отступом в два пробела:
```
MyValue :=
  MyValue + (SomeVeryLongStatement / OtherLongStatement);
```

Каждое присвоение и каждое выражение должно располагаться на разных строках.
Правильно
```
a := b + c; 
Inc(Count); 
```
Неправильно
```
a := b + c; Inc(Count);
```

Имена переменных должны иметь стиль Camel Caps.
```
var
  MyData: Integer;
  MyString: string;
```

В объявлении массива перед и после квадратных скобок должны стоять пробелы.
```
type
  TMyArray = array [0..100] of Char;
```

Оператор if всегда должен располагаться по крайней мере на двух строках
Неправильно
```
if A < B then DoSomething; 
```

Правильно
```
if A < B then 
  DoSomething;
```

В случае составного оператора необходимо поместить каждый оператор на новую строку.
Неправильно
```
  if A < B then begin
    DoSomething;
    DoSomethingElse;
  end else begin
    DoThis;
    DoThat;
  end;
```

Правильно
```
  if A < B then
  begin
    DoSomething;
    DoSomethingElse;
  end
  else
  begin
    DoThis;
    DoThat;
  end;
```

Все остальные варианты расположения операторов не рекомендуются и не одобряются, хотя и являются синтаксически правильными. Избегайте использования круглых скобок в простых проверках. Например:

Правильно
```
  if I > 0 then 
    DoSomething;
```

Неправильно
```
  if (I > 0) then
    DoSomething;
```

Оператор for
Неправильно
```
for i := 0 to 10 do begin
    DoSomething; 
    DoSomethingElse;
  end;
```

Правильно
```
  for i := 0 to 10 do
  begin
    DoSomething;
    DoSomethingElse;
  end;

  for I := 0 to 10 do
    DoSomething;
```

Оператор while

Неправильно
```
  while x < j  do begin
    DoSomething;
    DoSomethingElse;
  end;
```

Правильно
```
  while x < j do
  begin
    DoSomething;
    DoSomethingElse;
  end;			
  while x < j do
    Something;
 ``` 

Оператор repeat until
Правильно
```
  repeat
    x := j;
    j := UpdateValue;
  until j > 25;
```

Оператор case
Несмотря на то, что существует множество синтаксически правильных конструкций, одобренной и рекомендованной считается следующая:
Правильно
```
 case ScrollCode of
    SB_LINEUP, SB_LINEDOWN:
      begin
        Incr := FIncrement div FLineDiv;
        FinalIncr := FIncrement mod FLineDiv;
        Count := FLineDiv;
      end;
    SB_PAGEUP, SB_PAGEDOWN:
      begin
        Incr := FPageIncrement;
        FinalIncr := Incr mod FPageDiv;
        Incr := Incr div FPageDiv;
        Count := FPageDiv;
      end;
  else
    Count := 0;
    Incr := 0;
    FinalIncr := 0;
  end;
```

Оператор try

Несмотря на то, что существует множество синтаксически правильных конструкций, одобренной и рекомендованной считается следующая:
Правильно
```
try
  try
    EnumThreadWindows(CurrentThreadID, @Disable, 0);
    Result := TaskWindowList;
  except 
    EnableTaskWindows(TaskWindowList);
    raise;
  end; 
finally
  TaskWindowList := SaveWindowList; 
  TaskActiveWindow := SaveActiveWindow;
end; 
```

## Разделы описаний Const, Var и Type

Зарезервированные слова `var`, `const` и `type` всегда пишутся на новой строке и не допускают появления на этой же строке какого-либо текста.

Правильно
```
type
  TMyType = Integer;

const
  MyConstant = 100;

var
  MyVar: Integer;
```

Неправильно
```
type TMyType = Integer;

const MyConstant = 100;

var MyVar: Integer;
```
